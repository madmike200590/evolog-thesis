The goal of this chapter is to evaluate the implementation of Evolog described in Chapter~\ref{chap:implementation} as a potential tool for application programming as described in Section~\ref{sec:problem-statement}. To that end, a complete application including user interaction, reading from and writing to files, as well as solving NP-hard search problems, is implemented purely in the Evolog language as introduced in the previous chapters.
Specifically, the application in question, which we will refer to as \emph{XML-graph-coloring} in the following sections, calculates 3-colorings for graphs, which are read from XML files. Calculated 3-colorings are again written to XML files. The file locations for both input and output files are interactively entered by the user on the commandline during runtime. More specifically, the application implements the following overall workflow:
\begin{itemize}
    \item Upon startup, users are prompted to enter a path to a file containing an XML-encoded graph.
    \item The given file is read using \emph{Action Rules} as described in Section~\ref{sec:evolog-actions}.
    \item The XML data read from the input file is parsed into a function-term based representation using a dedicated xml-parsing \emph{Module} (see Section~\ref{sec:evolog-modules}.)
    \item Based on the graph representation from the previous step, another module is used to calculate 3-colorings of the parsed graph.
    \item Calculated 3-colorings are encoded as XML strings using a dedicated Evolog module.
    \item The user is prompted to enter a file path to which the XML-encoded graph colorings should be written.
    \item Generated colorings are written to the file indicated by the user.
\end{itemize}    
The following section describes the XML-graph-coloring application in detail.

\section{Interactive XML-based Graph Coloring}
\label{sec:results-xml-graphcol}

From an application architecture point of view, the XML-graph-coloring application consists of the following main blocks:
\begin{itemize}
    \item Overall control flow, including user interaction and input/output actions.
    \item XML parsing logic, encapsulated in Module \texttt{xml\_dom}.
    \item Graph 3-coloring calculation, implemented in Module \texttt{threecol}.
    \item Encoding of graph colorings as XML strings, which is realized in Module \texttt{threecol\_to\_xml}.
\end{itemize}    
Since both the \texttt{xml\_dom} as well as the \texttt{threecol\_to\_xml} module are realtively verbose and basically only deal with ordering elements, these are referred to as black boxes here. A detailed discussion including source code is given in Appendix~\ref{sec:appendix-xml-dom-parsing} and~\ref{sec:appendix-TODO}. The implementation of the three-coloring module is the same as shown in Example and is therefore not discussed again here.

\subsection{Reading files from user-supplied paths}

Following the control flow of the application as a user percieves it, first, the user is asked to enter a file path. This file's content is then read line-by-line and aggregateed into a list term. Listing~\ref{lst:results-xml-graphcol-userinput} shows this part of the program.

\begin{lstlisting}[style=asp-code, label={lst:results-xml-graphcol-userinput}, caption={Reading a file based on user input.}] 
enter_input_prompt("Please enter a path to read graphs from: ").

write_input_prompt_res(R) : @streamWrite[STDOUT, PROMPT] = R :- 
    enter_input_prompt(PROMPT), &stdout(STDOUT).
usr_input_res(INP) : @streamReadLine[STDIN] = INP :- 
    write_input_prompt_res(success(_)), &stdin(STDIN).

infile(PATH) :- usr_input_res(success(line(PATH))).
infile_open(PATH, HD) : @fileInputStream[PATH] = HD :- 
    infile(PATH).

% Handle file opening error
error(io, MSG) :- infile_open(_, error(MSG)).

% Read all lines from infile 
readline_result(PATH, 0, RES) : 
    @streamReadLine[STREAM] = RES :- 
    infile(PATH), infile_open(PATH, success(stream(STREAM))).
readline_result(PATH, LINE_NO, RES) : 
    @streamReadLine[STREAM] = RES :- 
    infile(PATH), 
    infile_open(PATH, success(stream(STREAM))), 
    readline_result(PATH, PREV_LINE_NO, PREV_LINE_RES), 
    PREV_LINE_RES != success(line(eof)), 
    LINE_NO = PREV_LINE_NO + 1.

% close stream after getting eof
infile_closed(PATH, RES) : 
    @inputStreamClose[STREAM] = RES :- 
    infile(PATH), 
    infile_open(PATH, success(stream(STREAM))),
    readline_result(PATH, _, ok(eof)).

% Now create a list of content lines
file_lines(PATH, LST) :- 
    LST = #list{ 
        line(LINE_NO, LINE) : 
            readline_result(PATH, LINE_NO, success(line(LINE))), 
            LINE != eof },
    infile(PATH). 
\end{lstlisting}    

The rule on line 3 derives the predicate \texttt{write\_input\_prompt\_res/1}, which wraps the result of a stream output operation - which in this case writes a prompt text to stdout. Note that the \emph{standard output stream} \texttt{STDOUT} does not need to be opened using an Evolog action, but is always available to an application. A reference to \texttt{STDOUT} can be obtained through the external atom \texttt{stdout/1}.

If writing the prompt text succeeds, i.e. in case the result term of the write action has structure \texttt{success(\_)}, user input is read from the \emph{standard input stream} \texttt{STDIN}. The result of the read operation is derived on line 5 using  predicate \texttt{usr\_input\_res/1}. Note that, as with stdout, \texttt{STDIN} does not have to be opened, but is obtained using the external atom \texttt{stdin/1}.

In case the read operation succeeded, we attempt to open an input stream on the file entered by the user. The rule on line 9 derives the result of this operation in the \texttt{infile\_open/2} predicate. The first term is the path of the respective file, the second is an action result term which either takes the form \texttt{error(MSG)} or holds a reference to a file input stream \texttt{STREAM} wrapped in a function term \texttt{success(stream(STREAM))}.

If opening succeeds, the openend file is read line-wise, using the rules on lines 16 and 19. Lines are numbered in order to keep positional information of file content. Reading stops once the result from the last \texttt{@streamReadLine} action is \texttt{success(line(eof))} where \texttt{eof} denotes \emph{end-of-file}.

After all lines of the input file have been read successfully an \texttt{@inputStreamClose} action is used to close the input file (see line 28). Finally, all lines from a file are aggregated into a single list term using a \texttt{\#list} aggregate in the rule on line 35.

\subsection{Parsing XML data}
\label{subsec:results-xml-parsing}

Based on the list of lines in an input file, the contained XML data is parsed using Module \texttt{xml\_dom}, which is described in detail in Appendix~\ref{sec:appendix-xml-dom-parsing}. Input files are expected to conform to the \gls{dtd} in Listing\ref{lst:results-xml-graphcol-input}.

\begin{lstlisting}[style=asp-code, label={lst:results-xml-graphcol-input}, caption={\gls{dtd} for graph XML files.}]  
<!ELEMENT graph (vertices, edges)>
<!ATTLIST graph directed (true | false) #REQUIRED>

<!ELEMENT vertices (vertex+)>
<!ELEMENT vertex (#PCDATA)>

<!ELEMENT edges (edge+)>
<!ELEMENT edge (source, target)>
<!ELEMENT source (#PCDATA)>
<!ELEMENT target (#PCDATA)>    
\end{lstlisting}    

Listing~\ref{lst:results-xml-graphcol-input-ex} shows an example input file conforming to the \gls{dtd} which describes the complete graph $K_3$.

\begin{lstlisting}[style=asp-code, label={lst:results-xml-graphcol-input-ex}, caption={The complete graph $K_3$ represented according to the \gls{dtd} from Listing~\ref{lst:results-xml-graphcol-input}.}]
<graph directed="false">
    <vertices>
        <vertex>a</vertex>
        <vertex>b</vertex>
        <vertex>c</vertex>
    </vertices>
    <edges>
        <edge>
            <source>a</source>
            <target>b</target>
        </edge>
        <edge>
            <source>b</source>
            <target>c</target>
        </edge>
        <edge>
            <source>c</source>
            <target>a</target>
        </edge>
    </edges>    
</graph>
\end{lstlisting} 

The XML-parsing module has 4 output terms which represent lists of \gls{dom} elements, parent-child relationships between \gls{dom} elements, text content of elements as well as attributes, respectively. Listing~\ref{lst:results-xml-graphcol-dom-unpack} shows the Evolog code to parse the XML content of an input file. Since each of the four result terms of the \texttt{xml\_dom} module is a list term, in order to have one atom per list entry, each list needs to be "unwrapped". List terms are unwrapped by first deriving one "entry"-atom for every element of the list which holds the element itself along with all successor elements in the list (i.e. the "tail list"). Finally, the tail list of each entry atom is projected away using an additional rule, such that tehre is one atom for each element of the original list. In Listing~\ref{lst:results-xml-graphcol-dom-unpack}, this unwrapping is performed for each output list of the XML parsing module from line 8 onward.

\begin{lstlisting}[style=asp-code, label={lst:results-xml-graphcol-dom-unpack}, caption={Parsing and consuming XML data in an Evolog program.}]
% Parse the string content of individual lines into a 
% DOM-like representation
file_dom(PATH, dom(ELEMENTS, CHILDREN, TEXTS, ATTRIBUTES)) :- 
    file_lines(PATH, LINES), 
    #xml_dom[LINES](ELEMENTS, CHILDREN, TEXTS, ATTRIBUTES).

% Unwrap list elements from module output
dom_element_entry(NAME, ID, TAIL) :- 
    file_dom(_, dom(lst(dom_element(NAME, ID), TAIL), _, _, _)).
dom_element_entry(NAME, ID, TAIL) :- 
    dom_element_entry(_, _, lst(dom_element(NAME, ID), TAIL)).
dom_element(NAME, ID) :- 
    dom_element_entry(NAME, ID, _).

dom_element_child_entry(NAME, ID, CHILD_ID, TAIL) :- 
    file_dom(_,dom(_, lst(
        dom_element_child(NAME, ID, CHILD_ID), TAIL), _, _)).
dom_element_child_entry(NAME, ID, CHILD_ID, TAIL) :- 
    dom_element_child_entry(_, _, _, lst(
        dom_element_child(NAME, ID,CHILD_ID), TAIL)).
dom_element_child(NAME, ID, CHILD_ID) :- 
    dom_element_child_entry(NAME, ID, CHILD_ID, _).

dom_element_text_entry(NAME, ID, TEXT, TAIL) :- 
    file_dom(_,dom(_, _, lst
    (dom_element_text(NAME, ID, TEXT), TAIL), _)).
dom_element_text_entry(NAME, ID, TEXT, TAIL) :- 
    dom_element_text_entry(_, _, _, lst(
        dom_element_text(NAME, ID, TEXT), TAIL)).
dom_element_text(NAME, ID, TEXT) :- 
    dom_element_text_entry(NAME, ID, TEXT, _).

dom_element_attribute_entry(
    NAME, ID, ATTR_NAME, ATTR_VALUE, TAIL) :- 
    file_dom(_,dom(_, _, _, lst(
        dom_element_attribute(
            NAME,ID, ATTR_NAME, ATTR_VALUE), TAIL))).
dom_element_attribute_entry(
    NAME, ID, ATTR_NAME, ATTR_VALUE, TAIL) :- 
    dom_element_attribute_entry(_, _, _, lst(
        dom_element_attribute(
            NAME, ID, ATTR_NAME, ATTR_VALUE), TAIL)).
dom_element_attribute(NAME, ID, ATTR_NAME, ATTR_VALUE) :- 
    dom_element_attribute_entry(
        NAME, ID, ATTR_NAME, ATTR_VALUE, _).   
\end{lstlisting}  

\subsection{Extracting a graph representation from parsed XML data}
\label{subsec:results-xml-extract-graph}

Based on the DOM-elements unwrapped in Listing~\ref{lst:results-xml-graphcol-dom-unpack}, a representation of a graph using predicates \texttt{graph/1}, \texttt{graph\_vertex/2}, \texttt{graph\_directedness/2} and \texttt{graph\_edge/2} is constructed using the code from Listing~\ref{lst:results-xml-graphcol-build-graph}. DOM elements are translated into vertices and edges based on their names. Whether the graph in question is directed or undirected can be extracted from the corrseponding attribute of the \texttt{graph} element.

\begin{lstlisting}[style=asp-code, label={lst:results-xml-graphcol-build-graph}, caption={Constructing a graph representation from parsed XML data.}]
% Translate generic DOM-elements into graphs
graph(ID) :- dom_element("graph", ID).
graph_vertex(ID, VERTEX) :- 
    graph(ID), dom_element("graph", ID), dom_element_child("graph", ID, VERTEX_LIST_ID), 
    dom_element("vertices", VERTEX_LIST_ID), dom_element("vertex", VERTEX_ID), dom_element_child("vertices", VERTEX_LIST_ID, VERTEX_ID),
    dom_element_text("vertex", VERTEX_ID, VERTEX).
graph_directedness(ID, DIRECTED) :- 
    graph(ID), dom_element("graph", ID), dom_element_attribute("graph", ID, "directed", DIRECTED).
graph_undirected(ID) :- graph_directedness(ID, "false").
graph_directed(ID) :- graph_directedness(ID, "true").
graph_edge(ID, EDGE) :- 
    graph(ID), graph_vertex(ID, SRC_ID), graph_vertex(ID, TARGET_ID), dom_element("graph", ID), dom_element_child("graph", ID, EDGE_LIST_ID), 
    dom_element("edges", EDGE_LIST_ID), dom_element("edge", EDGE_ID), dom_element_child("edges", EDGE_LIST_ID, EDGE_ID),
    dom_element("source", SRC_ELEM_ID), dom_element_child("edge", EDGE_ID, SRC_ELEM_ID), dom_element_text("source", SRC_ELEM_ID, SRC_ID),
    dom_element("target", TARGET_ELEM_ID), dom_element_child("edge", EDGE_ID, TARGET_ELEM_ID), dom_element_text("target", TARGET_ELEM_ID, TARGET_ID),
    EDGE = e(SRC_ID, TARGET_ID).
graph_edge(ID, e(TARGET, SRC)) :- graph_edge(ID, e(SRC, TARGET)), graph_undirected(ID).	    
\end{lstlisting}    

\subsection{Calculating and storing graph colorings}

Colorings for the graph parsed in the previous sections can be calculated using module \texttt{threecol} introduced in Example~\ref{ex:3col-module}. For later processing (i.e. serialization to XML content), each calculated coloring is assigned a unique ID using the enumeration predicate \texttt{ids/3}.

\begin{lstlisting}[style=asp-code, label={lst:results-xml-graphcol-colorings}, caption={Calculating graph colorings.}]
graph_threecol(ID, COL) :- 
	graph(ID), 
	VERTEX_LIST = #list{V : graph_vertex(ID, V)}, 
	EDGE_LIST = #list{edge(V1, V2) : graph_edge(ID, e(V1, V2))}, 
	#threecol[VERTEX_LIST, EDGE_LIST](COL).

graph_coloring(GRAPH, COLORING, ID) :- ids(coloring_ids, gcol(GRAPH, COLORING), ID), graph_threecol(GRAPH, COLORING).    
\end{lstlisting}    

In order to write calculated colorings to an XML file, each coloring is first converted to an XML string using module \texttt{threecol\_to\_xml}, which is shown in Listing~\ref{lst:results-xml-graphcol-serialization}. The module takes as input an atom of predicate \texttt{coloring/1} which is assumed to have one argument term, where the argument is a list of terms representing vertices along with an assigned color, e.g. \texttt{coloring(lst(col(1, red), lst(col(2, blue), lst\_empty))).}
In the listing, the rules on lines 2 through 8 are used to first "unwrap" the input list into one \texttt{coloring\_entry/2} atom per list entry, an then establish a total ordering of said atoms using predicates \texttt{coloring\_entry\_pred/2}, \texttt{coloring\_entry\_first/1} and \texttt{coloring\_entry\_last/1}. Based on the derived ordering, an XML string representing the input coloring is built recursively starting from the "lowest" element. Every step of the construction process yields an instance of \texttt{coloring\_xmlstr\_upto/2} where the second argument term is the identifier of the "highest" vertex encoded in the XML string constituting the first argument. The output predicate of the module \texttt{coloring\_xmlstr/1} is derived from the "maximal" instance of \texttt{coloring\_xmlstr\_upto/2}, i.e. the one corresponding to the last coloring entry according to \texttt{coloring\_entry\_last/1}.

Listing~\ref{lst:results-xml-graphcol-stringify} shows how module \texttt{threecol\_to\_xml} is used in the application to obtain an XML string for each calculated graph coloring.

\begin{lstlisting}[style=asp-code, label={lst:results-xml-graphcol-stringify}, caption={Generating XML strings for graph colorings.}]
% Translate colorings into strings
stringified_coloring_lst(GRAPH, COL_ID, COLSTR_LST) :- 
    graph_coloring(GRAPH, COLS, COL_ID),
    #threecol_to_xml[COLS](COLSTR_LST).
stringified_coloring_entry(GRAPH, COL_ID, COL_STR, TAIL) :-
    stringified_coloring_lst(GRAPH, COL_ID, lst(coloring_xmlstr(COL_STR), TAIL)).
stringified_coloring_entry(GRAPH, COL_ID, COL_STR, TAIL) :-
    stringified_coloring_entry(GRAPH, COL_ID, _, lst(coloring_xmlstr(COL_STR), TAIL)).	
stringified_coloring(GRAPH, COL_ID, COL_STR) :- stringified_coloring_entry(GRAPH, COL_ID, COL_STR, _).
\end{lstlisting}  

Concatenating the strings for each coloring of a respective graph into one XML string encoding all colorings found for the graph in question is done in much the same way as the string representations of individual colorings are constructed. Listing~\ref{lst:results-xml-graphcol-concat-colorings} illustrates this.

\begin{lstlisting}[style=asp-code, label={lst:results-xml-graphcol-concat-colorings}, caption={Concatenating XML strings of individual colorings.}]
% Stitch individual coloring strings into one string
stringified_coloring_before(GRAPH, COL_ID, COL_ID_AFTER) :-
    stringified_coloring(GRAPH, COL_ID, _),
    stringified_coloring(GRAPH, COL_ID_AFTER, _),
    COL_ID_AFTER > COL_ID.	
stringified_coloring_not_predecessor(GRAPH, COL_ID, COL_ID_AFTER) :-
    stringified_coloring_before(GRAPH, COL_ID, COL_ID_INTM),
    stringified_coloring_before(GRAPH, COL_ID_INTM, COL_ID_AFTER).
stringified_coloring_predecessor(GRAPH, COL_ID, COL_ID_AFTER) :-
    stringified_coloring_before(GRAPH, COL_ID, COL_ID_AFTER),
    not stringified_coloring_not_predecessor(GRAPH, COL_ID, COL_ID_AFTER).
stringified_coloring_has_predecessor(GRAPH, COL_ID) :- 
    stringified_coloring(GRAPH, COL_ID, _), 
    stringified_coloring_predecessor(GRAPH, _, COL_ID).	
stringified_coloring_first(GRAPH, COL_ID) :- 
    stringified_coloring(GRAPH, COL_ID, _), 
    not stringified_coloring_has_predecessor(GRAPH, COL_ID).	
stringified_coloring_has_successor(GRAPH, COL_ID) :- 
    stringified_coloring(GRAPH, COL_ID, _), 
    stringified_coloring_before(GRAPH, COL_ID, _).	
stringified_coloring_last(GRAPH, COL_ID) :-
    stringified_coloring(GRAPH, COL_ID, _),
    not stringified_coloring_has_successor(GRAPH, COL_ID).

xmlstring_upto(GRAPH, STR, COL_ID_FIRST) :-
    &stdlib_string_concat["<colorings>", COLSTR](STR),
    stringified_coloring_first(GRAPH, COL_ID_FIRST),
    stringified_coloring(GRAPH, COL_ID_FIRST, COLSTR).

xmlstring_upto(GRAPH, STR, COL_ID) :-
    stringified_coloring_predecessor(GRAPH, COL_ID_PRED, COL_ID),
    xmlstring_upto(GRAPH, PREV_STR, COL_ID_PRED),
    stringified_coloring(GRAPH, COL_ID, COLSTR),
    &stdlib_string_concat[PREV_STR, COLSTR](STR).

colorings_xmlstring(GRAPH, RESULT_STR) :-
    stringified_coloring_last(GRAPH, COL_ID),
    xmlstring_upto(GRAPH, STR, COL_ID),
    &stdlib_string_concat[STR, "</colorings>"](RESULT_STR).
\end{lstlisting}  

\subsection{Writing calculated graph colorings to XML files}

Once all colorings for the graph(s) from the input file have been calculated and transformed into XML representation, the last remaining step is to actually write those XML strings to files. The code used for this is shown in Listing~\ref{lst:results-xml-graphcol-write-output}. The way the user interaction is implemented is similar to how the input file path is obtained in Listing~\ref{lst:results-xml-graphcol-userinput}.

\begin{lstlisting}[style=asp-code, label={lst:results-xml-graphcol-write-output}, caption={Writing Graph colorings to XML files.}]
% Prompt user for a path to which to write the calculated colorings for each graph
write_output_prompt_res(G, R) : @streamWrite[STDOUT, P] = R :- 
	graph(G), enter_output_prompt(PROMPT), 
	&stdlib_string_concat[PROMPT, G](S),
	&stdlib_string_concat[S, ": "](P),
	&stdout(STDOUT).
usr_output_res(G, INP) : @streamReadLine[STDIN] = INP :- write_output_prompt_res(G, success(_)), &stdin(STDIN).

% Open the output file
outfile(G, PATH) :- usr_output_res(G, success(line(PATH))).
outfile_open(G, PATH, HD) : @fileOutputStream[PATH] = HD :- outfile(G, PATH).

% Write colorings to output file
write_colorings_result(G, R) : @streamWrite[HD, STR] = R :- 
	colorings_xmlstring(G, STR), 
	outfile(G, PATH), 
	outfile_open(G, PATH, success(stream(HD))).

% Close the output file
outfile_closed(G, RES) : @outputStreamClose[STREAM] = RES :- 
	outfile(G, PATH), write_colorings_result(G, success(_)), 
	outfile_open(G, PATH, success(stream(STREAM))).
\end{lstlisting}

\section{Usage of the graph coloring application}

In this Section, we demonstrate a few example inputs for the graph coloring application along with the resulting output files and answer sets.\todo{rephrase, it's actually just one due to space constraints.}

\begin{example}[Trivial Graph]
\label{ex:results-graphcol-trivial}
The simplest case on which to test our graph coloring application is a trivial graph consisting of one vertex. Listing~\ref{lst:results-graphcol-trivial-input} shows the corresponding input XML file.

\begin{lstlisting}[style=asp-code, label={lst:results-graphcol-trivial-input}, caption={One-vertex graph in XML encoding.}]
<graph directed="false">
    <vertices>
        <vertex>a</vertex>
    </vertices>
</graph>    
\end{lstlisting}  

Listing~\ref{lst:results-graphcol-trivial-call} shows the solver call to run the XML-Graph-Coloring application for the input as well as all inputs and outputs during runtime.

\begin{lstlisting}[style=asp-code, label={lst:results-graphcol-trivial-call}, caption={Running the application for a one-vertex graph.}]
C:\...\evolog-thesis\evolog-samples\xml-graphcol> alpha-solver -i .\file-based-3col.evl -i .\xml_dom.mod.evl -i .\3col-module-with-lists.evl -i .\3col_to_xml.mod.evl 
Please enter a path to read graphs from:
inputs/trivial.xml
Please enter a path to write calculated colorings for graph 1:
outputs/trivial.out.xml
Answer set 1:
{ ... }
\end{lstlisting}

The (single) answer set $A$ of the program is rather lengthy in its unfiltered form and is therefore given as the union of sets $A_{input},A_{dom},A_{col},A_{string},A_{output}$ in which we group atoms by functional area they relate to.
We denote as $A_{input}$ the set of atoms in $A$ which are derived by rules dealing with obtaining user input (i.e. the path from which to read graph specifications):
\begin{align*}
    A_{input} = \{&enter\_input\_prompt("Please\ enter\ a\ path\ to\ read\ graphs\ from:\ "),\\
                  &write\_input\_prompt\_res(success(ok)),\\
                  &usr\_input\_res(success(line("inputs/trivial.xml"))),\ infile("inputs/trivial.xml"),\\
                  &infile\_open("inputs/trivial.xml", success(stream(inputStream\_2))),\\
                  &readline\_result("inputs/trivial.xml",\ 0,\ success(line("<graph\ directed="false">"))),\\
                  &readline\_result("inputs/trivial.xml",\ 1,\ success(line("    <vertices>"))),\\
                  &readline\_result("inputs/trivial.xml",\ 2\, success(line("        <vertex>a</vertex>"))),\\
                  &readline\_result("inputs/trivial.xml",\ 3,\ success(line(" </vertices>"))),\\
                  &readline\_result("inputs/trivial.xml",\ 4,\ success(line("</graph>"))),\\
                  &readline\_result("inputs/trivial.xml",\ 5,\ success(line(eof))),\\
                  &infile\_closed("inputs/trivial.xml",\ success(closeResult(ok))),\\
                  &file\_lines("inputs/trivial.xml",\\
                  &~~~~lst(line(0,\ "<graph\ directed="false">"),\\
                  &~~~~lst(line(1,\ "    <vertices>"),\ lst(line(2,\ "        <vertex>a</vertex>"),\\
                  &~~~~lst(line(3,\ "       </vertices>"),\ lst(line(4,\ "</graph>"),\\
                  &~~~~lst\_empty))))))\}
\end{align*} 
Similarly, $A_{dom}$ contains all atoms in the answer set which model the \gls{dom} representation of the input XML file. For brevity, we do not list every individual list term of the $file\_dom$ instance (since the atoms resulting from unwrapping the list are part of $A_{dom}$ anyway). Also, helper atoms for partial list entries needed for unwrapping (such as $dom\_element\_entry$) are not included:
\begin{align*}
    A_{dom} = \{&dom\_element("graph",~1),\\
                &dom\_element("vertex",~3),\\
                &dom\_element("vertices",~2),\\
                &dom\_element\_attribute("graph",~1,~"directed",~"false"),\\
                &dom\_element\_child("graph",~1,~2),\\
                &dom\_element\_child("vertices",~2,~3),\\
                &dom\_element\_text("vertex",~3,~"a")\}
\end{align*}  

The subset $A_{col}$ holds atoms relating to the actual model of the graph as well as calculated colorings:
\begin{align*}
    A_{col} = \{&graph(1),~graph\_vertex(1,~"a"),~graph\_undirected(1),\\
                &graph\_vertices(1,~lst("a",~lst\_empty)),~graph\_edges(1,~lst\_empty),\\
                &graph\_compact(1,~lst("a",~lst\_empty),~lst\_empty),\\
                &graph\_coloring(1,~lst(col("a",~blue),~lst\_empty),~2),\\
                &graph\_coloring(1,~lst(col("a",~green),~lst\_empty),~3),\\
                &graph\_coloring(1,~lst(col("a",~red),~lst\_empty),~1)\}
\end{align*} 

$A_{string}$ holds all atoms derived by rules dealing with serializing our calculated colorings into a single XML string. Since there is a relatively high number of pure "helper" atoms that are only used to impose orderings etc., this set is given in a much abbreviated form:
\begin{align*}
    A_{string} = \{&\mathit{stringified\_coloring}(1,~1,\\
                   &~~"\mathit{<coloring><vertex>}\\
                   &~~~~\mathit{<id>a</id><color>red</color></vertex></coloring>}"),\\
                   &\mathit{stringified\_coloring}(1,~2,\\
                   &~~"\mathit{<coloring><vertex>}\\
                   &~~~~\mathit{<id>a</id><color>blue</color></vertex></coloring>}"),\\
                   &\mathit{stringified\_coloring}(1,~3,\\
                   &~~"\mathit{<coloring><vertex>}\\
                   &~~~~\mathit{<id>a</id><color>green</color></vertex></coloring>}"),\\
                   &\mathit{colorings\_xmlstring}(1,\\
                   &~~"\mathit{<colorings>}\\
                   &~~~~\mathit{<coloring>}\\
                   &~~~~~~\mathit{<vertex><id>a</id><color>red</color></vertex>}\\
                   &~~~~\mathit{</coloring>}\\
                   &~~~~\mathit{<coloring>}\\
                   &~~~~~~\mathit{<vertex><id>a</id><color>blue</color></vertex>}\\
                   &~~~~\mathit{</coloring>}\\
                   &~~~~\mathit{<coloring>}\\
                   &~~~~~~\mathit{<vertex><id>a</id><color>green</color></vertex>}\\
                   &~~~~\mathit{</coloring>}\\
                   &~~\mathit{</colorings>}")\}
\end{align*}

Last but not least, $A_{output}$ groups together all atoms derived while prompting users for output paths to write colorings to, as well as actually writing the output file(s):
\begin{align*}
    A_{output} = \{&\mathit{write\_output\_prompt\_res}(1,~\mathit{success}(\mathit{ok})),\\
                   &\mathit{usr\_output\_res}(1,~\mathit{success}(\mathit{line}(\mathit{"outputs/trivial.out.xml"}))),\\
                   &\mathit{outfile}(1,~\mathit{"outputs/trivial.out.xml"}),\\
                   &\mathit{outfile\_open}(1,~\mathit{"outputs/trivial.out.xml"},~\mathit{success}(\mathit{stream}(\mathit{outputStream}\_3))),\\
                   &\mathit{write\_colorings\_result}(1,~\mathit{success}(\mathit{ok})),\\
                   &\mathit{outfile\_closed}(1,~\mathit{success}(\mathit{ok}))\}
\end{align*}

The output file written to \texttt{trivial.out.xml} is given in Listing~\ref{lst:results-graphcol-trivial-output}.

\begin{lstlisting}[style=asp-code, label={lst:results-graphcol-trivial-output}, caption={XML encoding of caluclated colorings for the input file from Listing~\ref{lst:results-graphcol-trivial-input}}]
<colorings>
	<coloring>
		<vertex>
			<id>a</id>
			<color>red</color>
		</vertex>
	</coloring>
	<coloring>
		<vertex>
			<id>a</id>
			<color>blue</color>
		</vertex>
	</coloring>
	<coloring>
		<vertex>
			<id>a</id>
			<color>green</color>
		</vertex>
	</coloring>
</colorings>    
\end{lstlisting}
\end{example}
 

\section{Performance of solving Evolog programs}
\label{sec:results-performance-tests}

In order to gain some idea of Alpha's solving performance for Evolog programs, a slightly modified version of the graph coloring program has been used where all rules that read user input interactively (and therefore are blocking) were removed and instead facts were added for the \texttt{infile/1} and \texttt{outfile/2} predicates.

Alpha's solving performance for this modified version of the XML-based-3-coloring application has been benchmarked against calculating colorings for the same graphs represented as ASP facts and without writing calculated colorings to XML files.
Listing~\ref{lst:results-classic-3col-k3} shows the "classic ASP" representation used for the complete graph with 3 vertices $K_3$.

\begin{lstlisting}[style=asp-code, label={lst:results-classic-3col-k3}, caption={Pure-ASP version of a 3-coloring encoding for the graph $K_3$.}]
vertex(1).
vertex(2).
vertex(3).

edge(1,2).
edge(2,3).
edge(3,1).  

% Make sure edges are symmetric
edge(V2, V1) :- edge(V1, V2).

% Guess colors
red(V) :- vertex(V), not green(V), not blue(V).
green(V) :- vertex(V), not red(V), not blue(V).
blue(V) :- vertex(V), not red(V), not green(V).

% Filter invalid guesses
:- vertex(V1), vertex(V2), edge(V1, V2), red(V1), red(V2).
:- vertex(V1), vertex(V2), edge(V1, V2), green(V1), green(V2).
:- vertex(V1), vertex(V2), edge(V1, V2), blue(V1), blue(V2).

col(V, red) :- red(V).
col(V, blue) :- blue(V).
col(V, green) :- green(V).    
\end{lstlisting}

\subsection{Performance Test Results}
\label{subsec:results-performance-numbers}

The results of running the above described performance tests are summarized in Table~\ref{tbl:results-perftest}.

\begin{table}[H]
    \begin{center}
        \caption[Test Results for calculating 6 3-colorings per graph, once based on XML-input with XML-output (XML-3col) and once in plain ASP (Classic-3col)]{All execution times are given in seconds.}
        \label{tab:results-other}
        \begin{tabularx}{\textwidth} { 
            |>{\raggedright\arraybackslash}X 
            | >{\centering\arraybackslash}X
            | >{\centering\arraybackslash}X
            | >{\centering\arraybackslash}X
            | >{\raggedleft\arraybackslash}X| }
            \hline
            \textbf{Instance} & \textbf{Classic-3col} & \textbf{XML-3col} \\ \hlineB{3}
                K3 & 0.684 & 1.202 \\ \hline
                G8-05-4 & 0.731 & 1.520 \\ \hline
                G13-03-4 & 0.726 & 1.816 \\ \hline
                G13-05-4 & 0.694 & 1.871  \\ \hline
                G21-03-4 & 0.775 & 3.359  \\ \hline
                G34-02-3 & 0.805 & 10.318 \\ \hline
                G55-01-4 & 0.830 & 28.830  \\ \hline
        \end{tabularx}
    \end{center}
\end{table}