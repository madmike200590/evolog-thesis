In this chapter, we discuss observations from the expirements conducted in Chapter~\ref{chap:application-experiment}. It is split into two main parts: In the first part, we discuss recurring patterns in code written in Evolog as observed on the model application written in Chapter~\ref{chap:application-experiment}. In the second part of this chapter, we take a look at the performance metrics gained from the testing described in Section~\ref{sec:results-performance-tests}.

\section{Observations from writing Evolog code}
\label{sec:discussion-language-improvements}

Considering the example application discussed in Section~\ref{sec:results-xml-graphcol}, this section aims to highlight potential areas for improvement of the language.

\subsection{Unwrapping list terms}
As the examples in Section~\ref{sec:results-xml-graphcol} clearly show, there is a certain amount of boilerplate code involved whenever one has to "unpack" a list term obtained as an output term of a module literal. Since in most cases where list terms come into play, one actually wants to work on individual values on the list, we end up with many iterations of similar-looking code that is needed to transform one atom with a single list term into a set of "element atoms", each referring to one element of the list. An example of this is the subset of the 3-Coloring module (see Example~\ref{ex:3col-module}) where the module input (an atom referencing a list of vertices and a list of edges) is deconstructed in order to represent a graph using instances of predicates \texttt{vertex/1} and \texttt{edge/2}. The relevant part of the program is shown in Listing~\ref{lst:discussion-graph-unwrap}.

\begin{lstlisting}[style=asp-code, label={lst:discussion-graph-unwrap}, caption={Unwrapping list terms representing a graph}]
	vertex_element(V, TAIL) :- graph(lst(V, TAIL), _).
	vertex_element(V, TAIL) :- vertex_element(_, lst(V, TAIL)).
	vertex(V) :- vertex_element(V, _).
	edge_element(E, TAIL) :- graph(_, lst(E, TAIL)).
	edge_element(E, TAIL) :- edge_element(_, lst(E, TAIL)).
	edge(V1, V2) :- edge_element(edge(V1, V2), _).
\end{lstlisting}

The six lines of code shown in Listing~\ref{lst:discussion-graph-unwrap} deal with deconstructing two list terms, a vertex list and and edge list, respectively, into individual atoms such that each atom corresponds to one element of the list.
The transformation always follows the same pattern:
\begin{itemize}
    \item One rule derives an instance of an "element atom" from the head of the list, which is obtained by deconstructing the list term into the form \texttt{lst(V, TAIL)}, where \texttt{TAIL} is the rest of the list without the head value represented as variable \texttt{V}.
    \item A second rule recursively derives further list elements from element atoms by further deconstructing the \texttt{TAIL} terms of the individual element atoms. Repeated evaluation of this rule eventually leads to one element atom for every element of the original list term.
    \item Finally, a third rule derives the actual domain atoms which are of interest by projecting away the partial lists from the individual element atoms.
\end{itemize}    

Example~\ref{ex:discussion-graph-unwrapping} demonstrates this method of unwrapping list terms based on a list representation of the complete graph with 3 vertices $K_3$.

\begin{example}[List Unwrapping]
\label{ex:discussion-graph-unwrapping} 
Listing~\ref{lst:discussion-graph-unwrapping-example} shows the "graph unwrapping" program from Listing~\ref{lst:discussion-graph-unwrap} including a fact representing the complete graph $K_3$ encoded as two list terms.
\begin{lstlisting}[style=asp-code, label={lst:discussion-graph-unwrapping-example}, caption={Unwrapping the list representation of $K_3$.}]
	graph(
		lst(a, lst(b, lst(c, lst_empty))),
		lst(edge(a, b), lst(edge(b, c), lst(edge(c, a), lst_empty)))).
	
	vertex_element(V, TAIL) :- graph(lst(V, TAIL), _).
	vertex_element(V, TAIL) :- vertex_element(_, lst(V, TAIL)).
	vertex(V) :- vertex_element(V, _).
	edge_element(E, TAIL) :- graph(_, lst(E, TAIL)).
	edge_element(E, TAIL) :- edge_element(_, lst(E, TAIL)).
	edge(V1, V2) :- edge_element(edge(V1, V2), _).
\end{lstlisting}
Listing ~\ref{lst:discussion-graph-unwrapped-example} shows the (single) answer set of the program given in Listing~\ref{lst:discussion-graph-unwrap}
\begin{lstlisting}[style=asp-code, label={lst:discussion-graph-unwrapped-example}, caption={The unwrapped list representation of $K_3$.}]
Answer set 1:
	{ edge(a, b)
	edge(b, c)
	edge(c, a)
	edge_element(edge(a, b), lst(edge(b, c), lst(edge(c, a), lst_empty)))
	edge_element(edge(b, c), lst(edge(c, a), lst_empty))
	edge_element(edge(c, a), lst_empty)
	graph(
		lst(a, lst(b, lst(c, lst_empty))), 
		lst(edge(a, b), lst(edge(b, c), lst(edge(c, a), lst_empty))))
	vertex(a)
	vertex(b)
	vertex(c)
	vertex_element(a, lst(b, lst(c, lst_empty)))
	vertex_element(b, lst(c, lst_empty))
	vertex_element(c, lst_empty) }
\end{lstlisting}
\end{example}    

\paragraph{Generalizing list-unrwapping}
As Example~\ref{ex:discussion-graph-unwrapping} demonstrates, unwrapping a list term always takes 3 rules that only differ in predicate names. Definition~\ref{def:discussion-list-extraction} introduces a potential "syntacic sugar" solution we call an \emph{unwrap rule}.

\begin{definition}[List-Unwrapping-Rule]
\label{def:discussion-list-extraction}
Given an Atom $a$ with terms $t_1,\ldots,t_k,\ldots,t_n$, where $t_k$ is a list term, we define the following rule to be the \emph{unwrap rule for $t_k$}:
\[
	e(V)~\leftarrow~\#unwrap\{ V~|~t_k \},~a(t_1,\ldots,t_k,\ldots,t_n).
\]
In the unwrap rule, $e(V)$ is called an \emph{element atom}, where $V$ refers to one value from the list $t_k$.
We define the semantics of the above rule to be equivalent to the following program:
\begin{align*}
	\mathit{list\_element}(V,\mathit{TAIL})~&\leftarrow~a(t_1,\ldots,\mathit{lst}(V,\mathit{TAIL}),\ldots,t_n).\\
	\mathit{list\_element}(V,\mathit{TAIL})~&\leftarrow~\mathit{list\_element}(\_,\mathit{lst}(V,\mathit{TAIL})).\\
	e(V)~&\leftarrow~\mathit{list\_element}(V,\_).
\end{align*}	
\end{definition}

Listing~\ref{lst:discussion-sugared-unwrap} shows a potential implementation of the 3-Coloring Module from Example~\ref{ex:3col-module} using the shorthand notation for list unwrapping proposed in Definition~\ref{def:discussion-list-extraction}.

\begin{lstlisting}[style=asp-code, label={lst:discussion-sugared-unwrap}, caption={3-Coloring Module with shorthand list unwrapping rules.}]
#module threecol(graph/2 => {col/2})  {
	% Unwrap input
	vertex(V) :- #unwrap{V | VLST}, graph(VLST, _).
	edge(V1, V2) :- #unwrap{edge(V1, V2) | ELST}, graph(_, ELST).

	% Make sure edges are symmetric
	edge(V2, V1) :- edge(V1, V2).

	% Guess colors
	red(V) :- vertex(V), not green(V), not blue(V).
	green(V) :- vertex(V), not red(V), not blue(V).
	blue(V) :- vertex(V), not red(V), not green(V).

	% Filter invalid guesses
	:- vertex(V1), vertex(V2), edge(V1, V2), red(V1), red(V2).
	:- vertex(V1), vertex(V2), edge(V1, V2), green(V1), green(V2).
	:- vertex(V1), vertex(V2), edge(V1, V2), blue(V1), blue(V2).

	col(V, red) :- red(V).
	col(V, blue) :- blue(V).
	col(V, green) :- green(V).
}
\end{lstlisting}	

\subsection{Repetitive File-IO Actions}

As we can observe in Listings~\ref{lst:results-xml-graphcol-userinput} and~\ref{lst:results-xml-graphcol-write-output}, respectively, reading the content of a file, as well as writing to a file require a set of rules that looks the same regardless of the specific file or content in question. Listing~\ref{lst:discussion-fileio-readlines} shows the "standard program" to read all lines from a file:
\begin{itemize}
	\item A file path (i.e. location in storage), represented using the fact \texttt{infile(PATH)} is opened.
	\item Assuming opening the file produced no error, the first line of content is read using the rule on line 11.
	\item Based on the first line read (line number 0), we recursively continue reading as long as reading the last line was successful and did not yield \texttt{eof} (i.e. a pseudo-term denoting the end of content) as a result.
	\item If \texttt{eof} has been read, the file is closed, and lines are represented as atoms of form \texttt{line(LINE\_NO, LINE)}.
\end{itemize}	

\begin{lstlisting}[style=asp-code, label={lst:discussion-fileio-readlines}, caption={Reading all lines from a file.}]
infile_open(PATH, HD) : @fileInputStream[PATH] = HD :- 
	infile(PATH).

% Handle file opening error
error(io, MSG) :- infile_open(_, error(MSG)).

% Read all lines from infile 
readline_result(PATH, 0, RES) : 
	@streamReadLine[STREAM] = RES :- 
	infile(PATH), infile_open(PATH, success(stream(STREAM))).
readline_result(PATH, LINE_NO, RES) : 
	@streamReadLine[STREAM] = RES :- 
	infile(PATH), 
	infile_open(PATH, success(stream(STREAM))), 
	readline_result(PATH, PREV_LINE_NO, PREV_LINE_RES), 
	PREV_LINE_RES != success(line(eof)), 
	LINE_NO = PREV_LINE_NO + 1.

% close stream after getting eof
infile_closed(PATH, RES) : 
	@inputStreamClose[STREAM] = RES :- 
	infile(PATH), 
	infile_open(PATH, success(stream(STREAM))),
	readline_result(PATH, _, ok(eof)).

% Extract actual lines and numbers
line(LINE_NO, LINE) :- 
	readline_result(PATH, LINE_NO, success(line(LINE))), 
	LINE != eof.
\end{lstlisting}	

The code to write a set of lines to a file looks similar to how a file is read. Listing~\ref{lst:discussion-fileio-writelines} demonstrates the typical implementation. The following steps are needed:
\begin{itemize}
	\item First, the file to which content is to be written is opened.
	\item Assuming lines to be written are consecutively numbered and available as atoms of form \texttt{line(LINE\_NO, LINE)}, the first line (assumed to have an index of 0) is written.
	\item If the first line has been successfully written, all other lines are written using a recursive rule.
	\item We derive that all lines have been written when the line with maximum index has been written.
	\item Once \texttt{all\_lines\_written} has been derived, the file is closed.
	\item The file is also closed in case an error occurs during writing.
\end{itemize}	

\begin{lstlisting}[style=asp-code, label={lst:discussion-fileio-writelines}, caption={Writing lines to a file.}]
% Open the output file
open_result(PATH, RES) : @fileOutputStream[PATH] = RES :- outfile(PATH).

% Write lines in order of ascending line number
write_result(0, RES) : @streamWrite[STREAM, LINE] = RES :- open_result(PATH, success(stream(STREAM))), outfile(PATH), line(0,LINE).
write_result(LINE_NO, RES) : @streamWrite[STREAM, LINE] = RES :-  
	open_result(PATH, success(stream(STREAM))), 
	write_result(LINE_NO - 1, success(ok)), 
	outfile(PATH), line(LINE_NO, LINE).
all_lines_written :- write_result(LINE_NO, success(ok)), LINE_NO = #max{NUM : line(NUM, _)}.
	
% The file should be closed once all lines were successfully written	
should_close(PATH, STREAM) :- all_lines_written, open_result(PATH, success(stream(STREAM))), outfile(PATH).

% The file should also be closed when an IO error occurs during writing
should_close(PATH, STREAM) :- 
	write_result(_, error(_)),
	open_result(PATH, success(stream(STREAM))), outfile(PATH).

% Close the file
close_result(PATH, RES) : @outputStreamClose[STREAM] = RES :- 
	should_close(PATH, STREAM), 
	open_result(PATH, success(stream(STREAM))), 
	outfile(PATH).
\end{lstlisting}	

\paragraph{Module-based abstraction of actions}
As we've demonstrated in Listings~\ref{lst:discussion-fileio-readlines} and~\ref{lst:discussion-fileio-writelines}, frequently used file operations like reading all lines from and writing a set of lines to a file, form repetitive code patterns that would greatly benefit from some shortened abstraction. The obvious solutions seems to be to simply permit actions in Evolog Modules. However, this approach presents some challenges with regards to transparent semantics.

Recalling the demand for \emph{action transparency} (see Definition~\ref{def:evolog-actions-transparency}), i.e. that the result of every firing (ground) action rule must be part of an answer set, modules containing actions can not simple be used in module literals as they're described in Section~\ref{sec:evolog-modules}. Instead, we'd have to apply the definitions of action functions in rule heads to an Evolog Module and have a module function as an action interpretation function. Example~\ref{ex:discussion-fileio-actmodules} demonstrates this approach.

\begin{example}[Action Modules]
\label{ex:discussion-fileio-actmodules}	
In this example, we show a potential implementation for a module that takes a list of numbered strings and a string describing a file location as input, and writes all lines to the referenced file. Listing~\ref{lst:discussion-fileio-actmodule-impl} gives an example of how a module definition for a module containing actions might look.
\begin{lstlisting}[style=asp-code, label={lst:discussion-fileio-actmodule-impl}, caption={Prototypical definition of a module containing Evolog actions.}]
#action write_lines(lines/1, path/1 => (success: lines_written/1 | error: io_error/1)) {
	% Unwrap line list
	line(IDX, LINE) :- #unwrap{line(IDX, LINE) | LST}, lines(LST).
	% Unwrap 1-element-list path/1
	outfile(PATH) :- #unwrap{P | PLST}, path(PLST).
	
	% Open the output file
	open_result(PATH, RES) : @fileOutputStream[PATH] = RES :- outfile(PATH).

	% Write lines in order of ascending line number
	write_result(0, RES) : @streamWrite[STREAM, LINE] = RES :- open_result(PATH, success(stream(STREAM))), outfile(PATH), line(0, LINE).
	write_result(LINE_NO, RES) : @streamWrite[STREAM, LINE] = RES :-  
		open_result(PATH, success(stream(STREAM))), 
		write_result(LINE_NO - 1, success(ok)), 
		outfile(PATH), line(LINE_NO, LINE).
	all_lines_written :- write_result(LINE_NO, success(ok)), LINE_NO = #max{NUM : line(NUM, _)}.
		
	% The file should be closed once all lines were successfully written	
	should_close(PATH, STREAM) :- all_lines_written, open_result(PATH, success(stream(STREAM))), outfile(PATH).

	% The file should also be closed when an IO error occurs during writing
	should_close(PATH, STREAM) :- 
		write_result(_, error(_)),
		open_result(PATH, success(stream(STREAM))), outfile(PATH).

	% Close the file
	close_result(PATH, RES) : @outputStreamClose[STREAM] = RES :- 
		should_close(PATH, STREAM), 
		open_result(PATH, success(stream(STREAM))), 
		outfile(PATH).

	io_error("Failed opening file: " + MSG) :-
		open_result(_, error(MSG)).
	io_error("Failed writing line " + IDX + ": " + MSG) :-
		write_result(IDX, error(MSG)).
	io_error("Failed closing file: " + MSG) :-
		close_result(_, error(MSG)).

	lines_written(CNT) :- CNT = #count{N, R : write_result(N, R)}, not io_error(_).		
}	
\end{lstlisting}	
The action module proposed in Listing~\ref{lst:discussion-fileio-actmodule-impl} differs from a "regular" module in the following ways:
\begin{itemize}
	\item The module output definition \texttt{(success: lines\_written/1 | error: io\_error/1)} distinguishes between a success and an error case. 
	\item Since we assume action result terms to always be unary function terms with symbol either $\mathit{success}$ or $\mathit{error}$, the atoms of the given predicates would then be arguments of the respective (success or error) term in their termified form according to Definition~\ref{def:module-output-translation}.
	\item The above items imply that an action module result can only be success \emph{or} error, but not both. In an actual implementation of this concept, ways of enforcing this rule would need to be adressed. 
\end{itemize}
Listing~\ref{lst:discussion-fileio-actmodule-usage} shows a prototypical example of how an action module could be used.
\begin{lstlisting}[style=asp-code, label={lst:discussion-fileio-actmodule-usage}, caption={Prototypical usage of an action module providing an abstraction to write content to a file.}]
line(0, "lorem ipsum dolor").
line(1, "sit amet consectetur").
line(2, "adipiscing  elit")

write_result(R) : @write_lines[LINES, "/tmp/out.txt"] = R 
	:- LINES = #list{line(I, L) : line(I, L)}.
\end{lstlisting}		
\end{example}

While the approach to writing composite actions using a special form of modules sketched in Example~\ref{ex:discussion-fileio-actmodules} looks promising at first glance, we note that it also, potentially, projects away information about side-effects: Consider two distinct Frames $F_1$ and $F_2$, where in $F_1$ the \texttt{stream\_write} action in module \texttt{write\_lines} gives an error for line 1, and in $F_2$, the same action yields an error for line 2. In both frames, the result of the action rule in the top-level program is going to be an error, specifically \texttt{write\_result(io\_error("Failed writing line 1: error"))} for $F_1$, and \texttt{write\_result(io\_error("Failed writing line 2: error"))} (note that wrapping list terms mandated by module output translation were ignored since there is only one answer set holding one instance of \texttt{io\_error} anyway). While in this case the line on which writing failed is made transparent through the error message, in general, it would be possible to project away the information how many lines were written, i.e. two frames might yield different outcomes for the actions inside the module while yielding the exact same result for the overall program. Potential implementations of this concept would have to make allowances for this.


\section{Potential improvements to solving performance}

The performance test results from Section~\ref{subsec:results-performance-numbers} show a strong increase in runtime of the XML-3-Coloring application with increasing vertex count of the input graph. Intuitively, this seems easily explained - the XML-parsing module described in Section~\ref{sec:appendix-xml-dom-parsing} has a number of predicates whose (ground) instance count in an answer set is $O(n^2)$ with regards to the number $n$ of instances of the predicates they depend on. A typical example are rules like in Listing~\ref{lst:results-xml-parse-tag-pairs} where, in order to find pairs of corresponding opening and closing XML tags, we construct a partial ordering of tokens, resulting in $\frac{n^2 - n}{2} = \binom{n}{2}$ atoms for the relation predicate. Similar constructs occur multiple times in the XML parsing module. While information about the position of tokens in the input text relative to each other is necessary for the program, it would not have to be kept in memory at all times - the program in the XML parsing module is stratified, and the output predicates which are actually reported back to the calling program all reside in the uppermost stratum.

Based on the observations above, we could reasonably adapt Alpha's stratified evaluation algorithm (see Section~\ref{subsubsec:impl-stratified-eval}) to discard predicates from working memory that are no longer needed for evaluation (i.e. all instances of all directly dependent predicates have been calculated) and are not part of a module's output predicates, thereby reducing memory footprint and in turn reduce the amount of time spent on (fruitless) garbage collection cycles in the JVM running Alpha.